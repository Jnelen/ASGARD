#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

if( ! $?MMTSBDIR ) then
    echo " ERROR:  MMTSBDIR is not set !"
    echo " Set MMTSBDIR to the root of your MMTSB installation."
    echo " And include MMTSBDIR/perl and MMTSBDIR/bin in your path."
    goto error
endif

if( ! $?AMBERHOME ) then
    setenv AMBERHOME "`pwd`/.."
    echo " Warning: AMBERHOME is not set."
    echo "Caution: Setting AMBERHOME to $AMBERHOME !"
endif

if( ! $?SANDEREXEC ) then
    echo " Warning: SANDEREXEC is not set."
    if( ! $?TESTsander ) then
        set TESTsander = "../../bin/sander"
        setenv SANDEREXEC "$AMBERHOME/bin/sander"
    else
        setenv SANDEREXEC "$TESTsander"
    endif
    echo "Caution: Setting SANDEREXEC to $TESTsander !"
endif

if( ! $?DO_PARALLEL ) then
    setenv DO_PARALLEL " "
endif

# Temperature replica exchange of BPTI.
# The bpti.prmtop and bpti.pdb files were created by following
# the steps in the 'Basic tutorials' section of Chapter 1 of
# the manual.  bpti.leap.log specifies the exact commands used.
# The aarexAmber.pl command line is explained:
#-temp 2:300:350
# 2 temperature windows, the minimum at 300 K, the maximum at 350 K;
# any intermediate temperatures would be exponentially spaced
# and are determined by MMTSB.
#-par partop=bpti.prmtop
# parameters for the simulation:  partop is required and specifies
# the Amber prmtop filename.  See the usage
# statement of aarexAmber.pl and the online documentation at
# http://mmtsb.scripps.edu/doc for other options.
# (See also $MMTSBDIR/perl/ReXServer.pm as a last resort.)
#-mdpar cutoff=12.0,gb=tc,dynoutfrq=1,dynseed=1069,dynsteps=3
# MD parameters for the simulation:  the nonbonded cutoff is
# 12.0 Angstroms, ie, cut=12.0; the Generalized Born method is tc,
# ie, igb=1; the dynamics output frequency is every step, ie,
# ntpr=1; the dynamics random seed is 1069, ie, ig=1069;
# the dynamics steps between attempted temperature exchanges is 3,
# ie, mmtsb_iterations=3;
# See the usage statement of aarexAmber.pl and the online
# documentation at
# http://mmtsb.scripps.edu/doc for the available options.
# (See also $MMTSBDIR/perl/Amber.pm as a last resort.)
#-n 4
# the number of replica exchange steps is 4; the total number
# of MD steps is mmtsb_iterations times this value, 3 * 4 = 12.
#-amberlog mdout.bpti
# the Amber output filenames are mdout.bpti; these will be 
# in the aa1 and aa2 subdirectories of bpti.
#-dir bpti
# the working directory is bpti
#bpti.pdb
# the initial structure for the simulation in PDB format.

set dir = bpti
set output = mdout.bpti

aarexAmber.pl -temp 2:300:350 -par partop=bpti.prmtop -mdpar cutoff=12.0,gb=tc,dynoutfrq=1,dynseed=1069,dynsteps=3 -n 4 -amberlog $output -dir $dir bpti.pdb

../dacdif $dir/aa1/$output.save $dir/aa1/$output
../dacdif $dir/aa2/$output.save $dir/aa2/$output
../dacdif $dir/aa1/final.pdb.save $dir/aa1/final.pdb
../dacdif $dir/aa2/final.pdb.save $dir/aa2/final.pdb

/bin/rm -f fort.7 $dir/rex* $dir/aa*/restart 
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)

