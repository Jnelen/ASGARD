#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

../../check_TeraChem.x
if( $status > 0) then
  exit(0)
endif

set sander = "../../../bin/sander"
if( $?TESTsander ) then
    set sander = $TESTsander
endif

set can_do_mpi=0
if( `echo $sander | grep "MPI"` != '') then
 echo 'Using MPI version of sander'
 set can_do_mpi=1
endif

set mpi=0
if( ! $?DO_PARALLEL ) then
  set DO_PARALLEL=' '
  echo "Running test using file-based exchange (DO_PARALLEL not set)"
  echo "Parallelism treated within TeraChem"
else
  if( $can_do_mpi == 1 ) then
    echo "Running test using MPI data exchange (DO_PARALLEL is set)"
    echo "Parallelism treated within TeraChem"
    set mpi=1
    set numprocs = `$DO_PARALLEL echo | wc -l`
    if ( $numprocs > 1 ) then
	echo "This test requires >> mpirun -np 1 <<"
	echo "You specified >> mpirun -np $numprocs <<"
	exit 0
    endif
  else
    echo "Can not do an MPI run using serial version of sander."
    echo "Unset DO_PARALLEL to perform serial test"
    exit(1)
  endif
endif

cat > mdin <<EOF
Alanine dipeptide QM/MM HF/STO-3G/ff99SB NVE 2 steps (dt=0.5fs, no shake)
 &cntrl
  imin   = 0,           !no minimization
  irest  = 1,           !restart
  ntx    = 5,           !coordinates and velocities are read in
  cut    = 9999.9       !non-bonded interactions cutoff
  dt     = 0.0005,      !0.5fs time step
  ntb    = 0,           !no periodicity and PME off!
  ntt    = 0,           !turn off thermostat
  ntpr   = 1,           !print details to log every step
  ntwx   = 1,           !write coordinates to mdcrd every 500 steps (every 250fs)
  ntwr   = 2,           !write restart file at last step
  nstlim = 2,           !run for 2 steps
  nscm   = 0,           !No removal of COM motion,
  ioutfm = 1,           !NetCDF MDCRD.
  ifqnt  = 1,
 /
 &qmmm
  qmmask    = ':2',
  qm_theory = 'EXTERN',
  qmmm_int  = 5,
 /
 &tc
  method   = 'rhf',
  basis    = 'sto-3g',
  mpi      = $mpi,
  ngpus    = 0,
 /
EOF

set output = aladip.hf_sto-3g.MechEm.mdout
set restrt = aladip.hf_sto-3g.MechEm.restrt
rm -f $output

echo "mpi=$mpi"
if ( $mpi == 1 ) then
  mpirun -np 1 terachem -UseMPI > tc_job.dat &
endif

touch dummy
$DO_PARALLEL $sander -O -p aladip.prmtop -c aladip.restrt -o $output -r $restrt < dummy || goto error
grep -v 'mpi' $output | grep -v 'ngpus' | grep -v 'gpuids' | grep -v 'root_number' | grep -v 'executable' > $output.tmp
mv $output.tmp $output

# We do this due to rounding errors when reading from a file in the file-based 
# data exchange vs receiving the data through MPI
../../dacdif -a 0.0003  $output.save $output
../../dacdif -a 0.0003  $restrt.save $restrt

/bin/rm -f mdin mdinfo mdcrd dummy inpfile.xyz ptchrg.xyz startfile tc_job* old.tc*
/bin/rm -r scr
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)
