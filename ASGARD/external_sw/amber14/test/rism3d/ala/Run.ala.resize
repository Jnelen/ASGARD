#!/bin/csh -f
#TEST-PROGRAM sander
#TEST-DESCRIP TO_BE_DEtermined
#TEST-PURPOSE regression, basic
#TEST-STATE   undocumented

echo "SANDER: 3D-RISM w/ Force Extrapolation & dynamic solvation box"

if( ! $?TESTsander ) set TESTsander = "../../../bin/sander"

if ! { ../checkrismunsupported.sh $TESTsander } exit 0

if( ! $?DO_PARALLEL ) then
        setenv DO_PARALLEL " "
else
        set numprocs=`echo $DO_PARALLEL | awk -f ../../numprocs.awk `
        if ( $numprocs > 3 ) then
            echo " DO_PARALLEL set to $DO_PARALLEL"
            goto toomany
        else if ( $?MP_PROCS)then
            if ($MP_PROCS > 3)then
#               --- For IBM AIX ---
                echo "    MP_PROCS set to "$MP_PROCS
                goto toomany
            endif
        endif
endif

cat > mdin <<EOF
Alanine dipeptide run
 &cntrl
    ntx=1, ntpr=5, ntwx=0,ntwr=0
    nstlim=50,dt=0.001,
    ntt=3, ig=314159, tempi=0,gamma_ln=10,temp0=300, tempi=300,
    ntp=0,
    ntc=2,ntf=2,
    ntb=0,
    nrespa=1,
    irism=1
    cut=999,
/
&rism
    rismnrespa=5
    fcenbasis=5,fcestride=2,fcecrd=2
    fcetrans=4
    fcenbase=5
    tolerance=1d-8
    buffer=10
    mdiis_method=2
    verbose=1
 /
EOF

$DO_PARALLEL $TESTsander -O -i mdin -o ala.resize.out \
   -p ala.parm7 -c ala.rst7 -xvv ../../../AmberTools/test/rism1d/spc-kh/spc.xvv.save \
   || goto error

$AMBERHOME/AmberTools/test/nab/checkrism3d -err 1.e-6 ala.resize.out.save ala.resize.out
/bin/rm -f mdin mdinfo restrt
exit(0)

error:
echo "  ${0}:  Program error"
exit(1)

toomany:
    echo " too many processors for this test, exiting"
    echo "============================================================"
    exit(0)

